plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'ch.geowerkstatt.ilivalidator.extensions.functions'
archivesBaseName = 'geow-interlis-functions'
version = '0.0.0-SNAPSHOT'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    maven { url "https://jars.interlis.ch" }
}

dependencies {
    implementation group: 'ch.interlis', name: 'iox-api', version: '1.0.4'
    implementation group: 'ch.interlis', name: 'iox-ili', version: '1.21.8'
    implementation group: 'ch.interlis', name: 'ili2c-tool', version: "5.2.3"
    implementation group: 'ch.interlis', name: 'ili2c-core', version: "5.2.3"
    implementation group: 'com.vividsolutions', name: 'jts-core', version: '1.14.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

java{
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications{
        GeowerkstattCustomFunctions(MavenPublication){
            from components.java
            pom {
                name = 'geow-interlis-functions'
                description = 'Library containing function implementations for ilivalidator / INTERLIS v2.4'
                url = 'https://github.com/GeoWerkstatt/geow-interlis-functions'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v2.1'
                        url = 'https://github.com/GeoWerkstatt/geow-interlis-functions/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'GeoWerkstatt'
                        name = 'GeoWerkstatt GmbH'
                        email = 'office@geowerkstatt.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git:https://github.com/GeoWerkstatt/geow-interlis-functions.git'
                    developerConnection = 'scm:git:git:https://github.com/GeoWerkstatt/geow-interlis-functions.git'
                    url = 'https://github.com/GeoWerkstatt/geow-interlis-functions/'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/GeoWerkstatt/geow-interlis-functions')
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            username = property("OSSRH_USER")
            password = property("OSSRH_PASSWORD")
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing{
    def key = property("SIGNING_KEY")
    def password = property("SIGNING_PASSWORD")

    useInMemoryPgpKeys(key, password)
    sign(publishing.publications.GeowerkstattCustomFunctions)
}